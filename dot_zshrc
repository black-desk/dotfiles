# vim: ft=zsh

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# ==============================================================================
# Utilities
# ==============================================================================

function remove_windows_path() {
        setopt EXTENDED_GLOB
        export windows_path=(${(M)path:#/mnt/c/*})
        path=(${(M)path:#^/mnt/c/*})
        unsetopt EXTENDED_GLOB
}

function add_windows_path() {
        path=($path $windows_path)
        unset windows_path
}

function update_path() {
        local dir=$1

        if ! [ -d "$dir" ]; then
                return
        fi

        local append=$2

        if [[ ${path[(ie)$dir]} -gt ${#path} ]]; then 
                if [[ -n $append ]]; then
                        path+=($dir)
                else
                        path=($dir $path)
                fi
        fi
}

# ==============================================================================
# Environment
# ==============================================================================

brew_prefix=/opt/homebrew/bin

if [ ! -d "$brew_prefix" ]; then
        brew_prefix=/usr/local/bin
fi

if [ ! -d "$brew_prefix" ]; then
        brew_prefix=/home/linuxbrew/.linuxbrew/bin
fi

if [ -d "$brew_prefix" ] && [ -x "$brew_prefix/brew" ]; then
        eval "$("$brew_prefix/brew" shellenv)"
fi

unset brew_prefix


if [ command -v gpg &>/dev/null ]; then
        export GPG_TTY=$(tty)
fi

if locale -a | grep -q zh_CN.UTF-8; then
        export LANG=zh_CN.UTF-8
        export LANGUAGE=zh_CN
fi

if locale -a | grep -q en_US; then
        export LANGUAGE="$LANGUAGE${LANGUAGE+:}en_US"
fi

remove_windows_path

export HISTFILE="$HOME/.zsh_history"
export SAVEHIST=100000
export HISTSIZE=100000
setopt share_history

bindkey -v

bindkey "^r" history-incremental-search-backward
bindkey -s '^g' "lazygit^M"
bindkey -s '^h' "^[[D"
bindkey -s '^j' "^[[B"
bindkey -s '^k' "^[[A"
bindkey -s '^l' "^[[C"

export PAGER=less

# For command completion in sudo ...
# This is just a workaround.
alias sudo="sudo "

function ls () {
        if [[ "$PWD" == "$HOME" ]]; then
                /usr/bin/env ls -Isnap --color "$@"
                return
        fi
        /usr/bin/env ls --color "$@"
}

function la () {
        ls -la "$@"
}

setopt autopushd pushdsilent pushdtohome pushdignoredups pushdminus

update_path "/bin"
update_path "/sbin"
update_path "/usr/bin"
update_path "/usr/sbin"
update_path "/usr/local/bin"
update_path "/usr/local/sbin"
update_path "$HOME/.local/bin"

if command -v clang &>/dev/null; then
        export CC="clang"
fi

if command -v clang++ &>/dev/null; then
        export CXX="clang++"
fi

if command -v gdb &>/dev/null; then
        if $(cat /etc/os-release 2> /dev/null | grep Debian --quiet); then
                export DEBUGINFOD_URLS="https://debuginfod.debian.net"
        fi

        if $(cat /etc/os-release 2> /dev/null | grep Ubuntu --quiet); then
                export DEBUGINFOD_URLS="https://debuginfod.ubuntu.com"
        fi

        export GDBHISTFILE=.gdb_history
fi

if command -v cmake &>/dev/null; then
        if command -v ninja &>/dev/null; then
                export CMAKE_GENERATOR="Ninja"
        fi

        if command -v clang &>/dev/null && command -v clang++ &>/dev/null; then
                export CMAKE_TOOLCHAIN_FILE="$HOME/.config/blackdesk/cmake/LLVM.cmake"
        fi
fi

export CPM_SOURCE_CACHE="$HOME/.cache/CPM"

# ccache

if command -v ccache &>/dev/null; then
        update_path "/usr/lib/ccache"
        update_path "/home/linuxbrew/.linuxbrew/opt/ccache/libexec"
        update_path "/opt/homebrew/opt/ccache/libexec"
fi

update_path "$HOME/.local/share/JetBrains/Toolbox/scripts"

export GOPATH="$HOME/Documents/go"
update_path "$GOPATH/bin"

if command -v goenv &>/dev/null; then
        export GOENV_HOOK_PATH="$HOME/.local/lib/goenv_hook/"
        export GOENV_APPEND_GOPATH=
        export GOENV_PREPEND_GOPATH=1
        export GOENV_GOPATH_PREFIX="$GOPATH"
        eval "$(goenv init -)"
fi

if command -v pyenv &>/dev/null; then
        export PYENV_HOOK_PATH="$HOME/.local/lib/pyenv_hook/"
        eval "$(pyenv init -)"
fi

if command -v rbenv &>/dev/null; then
        eval "$(rbenv init -)"
fi

export N_PREFIX="$HOME/.n"
update_path "$N_PREFIX/bin"

update_path "$HOME/.cargo/bin"

update_path "$HOME/.ghcup/bin"
update_path "$HOME/.cabal/bin"

export TEXMFHOME="$HOME/.config/texmf"

if command -v fcitx5 &>/dev/null; then
        # fcitx5 do not have dbus activation feature,
        # so we start a fcitx5 service here but not replace existing daemon.
        (fcitx5 --keep -d &>/dev/null &)
fi

if command -v nvim &>/dev/null; then
        export VISUAL=nvim
        export EDITOR=nvim
fi

add_windows_path

# ==============================================================================
# P10K
# ==============================================================================

# If this shell is launching in a tty,
# I use fbterm to get UTF-8 character work.
[[ $(tty) == \/dev\/tty[0-6]* ]] &&
        command -v fbterm &>/dev/null &&
                fbterm -s 18 -n "MesloLGS NF" &&
                        exit

# If this shell is a ssh shell,
# I automatically start a tmux session `ssh`.
# But if the TERM_PROGRAM is WezTerm,
# I use the tabs of WezTerm instead of tmux to get tabs.
if [[ "$TERM_PROGRAM" != "WezTerm" ]] &&
        [[ -n "$PS1" ]] &&
        [[ -z "$TMUX" ]] &&
        [[ -n "$SSH_CONNECTION" ]] &&
        command -v tmux &>/dev/null; then

        ( tmux list-session 2>&1 | grep ssh ) && exec tmux attach-session -t ssh
        exec tmux new-session -s ssh
fi

CMD_EXE=/mnt/c/Windows/System32/cmd.exe

# WSL stuff.
if [[ -f $CMD_EXE ]]; then
        # I launch WSL by wsl.exe -d Arch -u root login -u ...
        # to make sure the settings in `pam_open_session` takes effect.
        #
        # But login will remove all environment variables setted by the WSL init.
        # So I have to launch another wsl shell to get them back.
        $CMD_EXE /C "wsl.exe -d Arch zsh -l -c env" 2>/dev/null |
                # this while loop is generated by bing chat
                while read line; do
                        if [[ $line == PWD* ]]; then
                                continue
                        fi
                        export $line
                done
        # The WSL_INTEROP is a special environment variable.
        # We cannot get it back from another wsl shell.
        # Check link below for more information.
        # https://github.com/microsoft/WSL/issues/5065#issuecomment-682198412
        for i in $(pstree -np -s $$ | grep -o -E '[0-9]+'); do
                if [[ -e "/run/WSL/${i}_interop" ]]; then
                        export WSL_INTEROP=/run/WSL/${i}_interop
                fi
        done
fi

if [[ -e $HOME/.nix-profile/etc/profile.d/hm-session-vars.sh ]]; then
	. $HOME/.nix-profile/etc/profile.d/hm-session-vars.sh
fi

if [[
        $(systemctl show --user gpg-agent-ssh.socket --property ActiveState 2>/dev/null) ==
        "ActiveState=active"
   ]]; then
        export SSH_AUTH_SOCK="$XDG_RUNTIME_DIR/gnupg/S.gpg-agent.ssh"
fi

# Setup zinit
# NOTE: copy from https://github.com/zdharma-continuum/zinit#manual
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

zinit depth=1 light-mode for \
        atinit"[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh" \
                romkatv/powerlevel10k

ZSH_AUTOSUGGEST_STRATEGY=(history completion)

# NOTE: copy from https://zdharma-continuum.github.io/zinit/wiki/Example-Minimal-Setup/
zinit wait lucid depth=1 light-mode for \
                agkozak/zsh-z \
        atinit"zicompinit; zicdreplay" \
                zdharma-continuum/fast-syntax-highlighting \
        atinit='ZSH_BASH_COMPLETIONS_FALLBACK_LAZYLOAD_DISABLE=true' \
                3v1n0/zsh-bash-completions-fallback \
        atload"_zsh_autosuggest_start" \
                zsh-users/zsh-autosuggestions \
        blockf atpull'zinit creinstall -q .' \
                zsh-users/zsh-completions \
                esc/conda-zsh-completion

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
